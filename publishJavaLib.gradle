apply plugin: 'maven-publish'

// 指定编码
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// Javadoc 配置
tasks.withType(Javadoc) {
    options {
        encoding "UTF-8"
        charSet 'UTF-8'
        links "http://docs.oracle.com/javase/8/docs/api"
    }
}

group = 'com.github.Potato-2020'
version = '1.1.6'

// 生成源码JAR
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

// 生成Javadoc JAR
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// JitPack 兼容配置
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'apiCompiler'
                description = 'API Compiler Library'
                url = 'https://github.com/Potato-2020/apiLogCompiler'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'Potato-2020'
                        name = 'Potato'
                    }
                }
            }
        }
    }
}